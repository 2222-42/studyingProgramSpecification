--
-- Module: register
--
-- Description: 
--
-- Version: 
-- Author: 
--

module register

--imports
--  from ModuleA all,
--  from ModuleB all

exports all

definitions

--state Sigma of
--  a : nat
--  b : nat
--  inv mk_Sigma(x,y) == x >= y
--  init s == s = mk_Sigma(0,0)
--end

types
    Name = token;
    Email = token;
    Address = token; Telephone = token;
    Info = Email * Address * Telephone;
    RegisterBook = map Name to Info;

values

operations

functions
    Add: Name * Info * RegisterBook -> RegisterBook
    Add(name, info, book) ==
        book munion {name |-> info}
    pre name not in set dom(book);

    AddAlt: Name * Email * Address * Telephone * RegisterBook -> RegisterBook
    AddAlt(name, email, address, telephone, book) ==
        book munion {name |-> mk_(email, address, telephone)}
    pre name not in set dom(book);

    Delete: Name * RegisterBook -> RegisterBook
    Delete(name, book) ==
        {name} <-: book
    pre name in set dom(book);

    Number: RegisterBook -> nat
    Number(book) ==
        card dom(book);

    FindInfo: Name * RegisterBook -> Info
    FindInfo(name, book) ==
        book(name)
    pre name in set dom(book);

    FindEmail: Name * RegisterBook -> Email
    FindEmail(name, book) ==
        let mk_(email, address, telephone) = book(name)
        in email
    pre name in set dom(book);

    FindEmailAlt: Name * RegisterBook -> Email
    FindEmailAlt(name, book) ==
        let mk_(email, -, -) = book(name)
        in email
    pre name in set dom(book);

    ChangeEmail: Name * Email * RegisterBook -> RegisterBook
    ChangeEmail(name, email, book) ==
        let mk_(-, address, telephone) = book(name)
        in book ++ {name |-> mk_(email, address, telephone)}
    pre name in set dom(book);

    ChangeEmailAlt: Name * Email * RegisterBook -> RegisterBook
    ChangeEmailAlt(name, email, book) ==
        let mk_(-, address, telephone) = book(name)
        in AddAlt(name, email, address, telephone, Delete(name, book))
    pre name in set dom(book);

end register
