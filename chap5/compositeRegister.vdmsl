--
-- Module: compositeRegister
--
-- Description: 
--
-- Version: 
-- Author: 
--

module compositeRegister

--imports
--  from ModuleA all,
--  from ModuleB all

exports all

definitions

--state Sigma of
--  a : nat
--  b : nat
--  inv mk_Sigma(x,y) == x >= y
--  init s == s = mk_Sigma(0,0)
--end

types
    Name = token;
    Email = token;
    Telephone = token;
    Info :: email: Email
            address: Address
            telephone: [Telephone]
            age: nat;

    Prefecture = token; City = token; Detail = token;
    Address :: prefecture: Prefecture
               city: City
               detail: Detail;

    RegisterBook = map Name to Info;

values

operations

functions
    Add: Name * Info * RegisterBook -> RegisterBook
    Add(name, info, book) ==
        book munion {name |-> info}
    pre name not in set dom(book);

    Delete: Name * RegisterBook -> RegisterBook
    Delete(name, book) ==
        {name} <-: book
    pre name in set dom(book);

    Number: RegisterBook -> nat
    Number(book) ==
        card dom(book);

    FindInfo: Name * RegisterBook -> Info
    FindInfo(name, book) ==
        book(name)
    pre name in set dom(book);

    FindNamesByPrefecture: Prefecture * RegisterBook -> set of Name
    FindNamesByPrefecture(prefecture, book) ==
        {n | n in set dom(book) &
                book(n).address.prefecture = prefecture};
    
    FindNamesByPrefectures: set of Prefecture * RegisterBook -> set of Name
    FindNamesByPrefectures(prefectures, book) ==
        {n | n in set dom(book) &
                book(n).address.prefecture in set prefectures};
    
    FindNamesByOverAge: nat * RegisterBook -> set of Name
    FindNamesByOverAge(age, book) ==
        {n | n in set dom(book) &
                book(n).age >= age};

    FindNamesByX: Prefecture * nat * RegisterBook -> set of Name
    FindNamesByX(prefecture, age, book) ==
        {n | n in set dom(book) &
                book(n).address.prefecture = prefecture and
                book(n).age >= age};

    FindNamesAltByX: Prefecture * nat * RegisterBook -> set of Name
    FindNamesAltByX(prefecture, age, book) ==
        FindNamesByPrefecture(prefecture, book) inter FindNamesByOverAge(age, book);

    FindEmail: Name * RegisterBook -> Email
    FindEmail(name, book) ==
        book(name).email
    pre name in set dom(book);

    ChangeEmail: Name * Email * RegisterBook -> RegisterBook
    ChangeEmail(name, email, book) ==
        book ++ {name |-> mu(book(name), email |-> email)}
    pre name in set dom(book);

end compositeRegister
